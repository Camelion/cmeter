buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE"
        classpath "com.netflix.nebula:gradle-extra-configurations-plugin:3.2.0"
    }
}


apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: "io.spring.dependency-management"
apply plugin: 'nebula.facet'
apply plugin: "nebula.optional-base"

facets {
    sample
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
group = "com.github.camelion"

jar {
    baseName = 'cmeter-clickhouse-exporter'
}

dependencies {
    compile project(":cmeter")

    compile 'ru.yandex.clickhouse:clickhouse-jdbc:0.1.25'
    compile "org.springframework.boot:spring-boot-autoconfigure", optional

    compileOnly "org.springframework.boot:spring-boot-configuration-processor"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

dependencyManagement {
    imports {
        mavenBom 'io.spring.platform:platform-bom:Cairo-BUILD-SNAPSHOT'
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'CMeter ClickHouse Exporter'
                packaging 'jar'
                description 'An artifact that provides ability to export metrics from CMeter to ClickHouse storage'
                url 'https://github.com/Camelion/cmeter'

                scm {
                    connection 'scm:git:https://github.com/Camelion/cmeter'
                    developerConnection 'scm:git:https://github.com/Camelion/cmeter.git'
                    url 'https://github.com/Camelion/cmeter'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'camelion'
                        name 'Camelion'
                        email 'camelion.c@mail.ru'
                    }
                }
            }
        }
    }
}